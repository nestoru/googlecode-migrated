# amz_products.xgawk
# Amazon product/node (or product/category) feeds parser
#  
#  
# author: Nestor Urquiza
# date: November 13, 2009
#
@load xml
BEGIN { 
  OFS= "\t"
  start_time = systime() 
  #test_counter = 0;
  print "asin", "title", "amount", "description", "lg_img", "md_img", "sm_img", "lg_height", "md_height", "sm_height", "lg_width", "md_width", "sm_width"
}

XMLSTARTELEM {
    depth++
    #test_counter ++;
    #if(test_counter == 100000){
    #  exit
    #}
}

XMLENDELEM   { depth-- }

XMLSTARTELEM  == "Item" {}

XMLSTARTELEM	== 	"SmallImage" { small = 1 }
XMLENDELEM  	== 	"SmallImage" { small = 0 }
XMLSTARTELEM  	== 	"MediumImage" { medium = 1 }
XMLENDELEM  	== 	"MediumImage" { medium = 0 }
XMLSTARTELEM  	== 	"LargeImage" { large = 1 }
XMLENDELEM  	== 	"LargeImage" { large = 0 }

XMLCHARDATA                { data  = $0    }
XMLENDELEM  == "ASIN"      { asin  = data  }
XMLENDELEM  == "Title"     { title  = data  }
XMLENDELEM  == "Amount"    { amount  = data }
XMLENDELEM  == "Content"    { description  = data }

XMLENDELEM  == "aws:URL"    { 
  if(small == 1) 	{ sm_img = data} 
  if(medium == 1) 	{ md_img = data} 
  if(large == 1) 	{ lg_img = data} 
}
XMLENDELEM  == "aws:Height"    { 
  if(small == 1)	{ sm_height = data}
  if(medium == 1)	{ md_height = data}
  if(large == 1)	{ lg_height = data}
}
XMLENDELEM  == "aws:Width"    { 
  if(small == 1)	{ sm_width = data}
  if(medium == 1)	{ md_width = data}
  if(large == 1)	{ lg_width = data}
}

#XMLENDELEM  == "BrowseNodeId"	{
#  if(depth == 4){
#    if(node != "")
#      node = node ","
#    node = node "" data
#  } 
#}

XMLENDELEM  == "Item" {
  print asin, title, amount, description, lg_img, md_img, sm_img, lg_height, md_height, sm_height, lg_width, md_width, sm_width
  data = asin = title = amount = description = lg_img = md_img = sm_img = lg_height = md_height = sm_height = lg_width = md_width = sm_width = ""
}
END{
  print "Took me " systime()-start_time " seconds"
}
