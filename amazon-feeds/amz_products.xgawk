# amz_products.xgawk
# Amazon product/node (or product/category) feeds parser
#  
#  
# author: Nestor Urquiza
# date: November 13, 2009
#
@load xml
BEGIN { 
  OFS= "\t"
  start_time = systime() 
  #test_counter = 0;
  #print "asin", "title", "price", "description", "lg_img", "md_img", "sm_img", "lg_height", "md_height", "sm_height", "lg_width", "md_width", "sm_width", "sku", "brand"
  print "asin", "title", "price", "description", "lg_img", "md_img", "sm_img", "sku", "brand", "sales_rank", "shipping_price", "offer_listing_id"
}

XMLSTARTELEM {
    depth++
    #test_counter ++;
    #if(test_counter == 100000){
    #  exit
    #}
}

XMLENDELEM   { depth-- }


XMLSTARTELEM	== 	"SmallImage" { small = 1 }
XMLENDELEM  	== 	"SmallImage" { small = 0 }
XMLSTARTELEM  	== 	"MediumImage" { medium = 1 }
XMLENDELEM  	== 	"MediumImage" { medium = 0 }
XMLSTARTELEM  	== 	"LargeImage" { large = 1 }
XMLENDELEM  	== 	"LargeImage" { large = 0 }
#XMLSTARTELEM    ==      "Source" { if(data == "Product Description") is_prod_desc = 1 }

XMLSTARTELEM    ==      "ItemPrice" { itemPrice = 1 }
XMLENDELEM      ==      "ItemPrice" { itemPrice = 0 }

XMLSTARTELEM    ==      "AmazonPrice" { amazonPrice = 1 }
XMLENDELEM      ==      "AmazonPrice" { amazonPrice = 0 }

XMLSTARTELEM    ==      "ShippingPrice" { shippingPrice = 1 }
XMLENDELEM      ==      "ShippingPrice" { shippingPrice = 0 }

XMLSTARTELEM    ==      "Item" {validReleaseDate = 0}

XMLCHARDATA                { data  = $0    }

XMLENDELEM  == "MerchantID" {
  if(amazonPrice == 1 && data == "ATVPDKIKX0DER"){
    validMerchantID = 1
  }else{
    #This needs to be reset at the end of the Item element as this is a child node affecting a parent: validMerchantID = 0
  }
  #print data "|" valid
}

XMLENDELEM  == "OfferListingID" {
  offerListingId = data
  if(data != ""){
    validOfferListingID = 1
  }else{
    validOfferListingID = 0
  }
  #print data "|" valid
}

XMLENDELEM  == "Availability" {
  if(data ~ /Usually ships in/){
    validAvailability = 1
  }else{
    validAvailability = 0
  } 
  #print data "|" valid
}

XMLENDELEM  == "ReleaseDate" {
  if(data != ""){
    validReleaseDate = 1
  }
  #print data "|" valid
}

XMLENDELEM  == "ASIN"      { asin  = data  }
XMLENDELEM  == "Title"     { title  = data  }
XMLENDELEM  == "Amount"    {
  if(amazonPrice == 1 && shippingPrice == 1){ 
    shippingPriceAmount = data
  }
  if(amazonPrice == 1 && shippingPrice != 1){
    amount  = data
  } 
}
XMLENDELEM  == "Content"   { 
  description = data
  gsub(/\t/, " ", description) 
}

XMLENDELEM  == "aws:URL"    { 
  if(small == 1) 	{ sm_img = data} 
  if(medium == 1) 	{ md_img = data} 
  if(large == 1) 	{ lg_img = data} 
}
#XMLENDELEM  == "aws:Height"    { 
#  if(small == 1)	{ sm_height = data}
#  if(medium == 1)	{ md_height = data}
#  if(large == 1)	{ lg_height = data}
#}
#XMLENDELEM  == "aws:Width"    { 
#  if(small == 1)	{ sm_width = data}
#  if(medium == 1)	{ md_width = data}
#  if(large == 1)	{ lg_width = data}
#}

XMLENDELEM  == "SKU"    { sku = data }
XMLENDELEM  == "Brand"    { brand = data }
XMLENDELEM  == "SalesRank"    { sales_rank = data }

#XMLENDELEM  == "BrowseNodeId"	{
#  if(depth == 4){
#    if(node != "")
#      node = node ","
#    node = node "" data
#  } 
#}

XMLENDELEM  == "Item" {
  #print asin, validMerchantID, validOfferListingID, validAvailability, validReleaseDate
  if(validMerchantID == 1 && validOfferListingID == 1 && validAvailability == 1 && validReleaseDate != 1){
    #print asin, title, amount, description, lg_img, md_img, sm_img, lg_height, md_height, sm_height, lg_width, md_width, sm_width
    print asin, title, amount, description, lg_img, md_img, sm_img, sku, brand, sales_rank, shippingPriceAmount, offerListingId
    #data = asin = title = amount = description = lg_img = md_img = sm_img = lg_height = md_height = sm_height = lg_width = md_width = sm_width = ""
  }
  data = asin = title = amount = description = lg_img = md_img = sm_img = sku = brand = sales_rank = shippingPriceAmount = offerListingId = ""
  validMerchantID = 0
}
END{
  #print "Took me " systime()-start_time " seconds"
}
